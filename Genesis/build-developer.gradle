plugins {
    id 'application'
    id 'eclipse'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.jlink' version '2.21.0'
    id "de.undercouch.download" version "4.0.0"
}

// Application Configuration
ext {
    appName = "Genesis-Devel"
    appLauncher = "Genesis-Development"
    appPackaging = "RPGFramework"
    appModuleName = "genesis"
    appMainClass = "genesis/org.prelle.genesis.Genesis5Main"
    profile = 'development'
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    mavenLocal()
    maven { url 'https://nexus.gluonhq.com/nexus/content/repositories/releases/' }
}

dependencies {
    implementation "org.apache.logging.log4j:log4j-api:2.11.0"
    implementation "org.apache.logging.log4j:log4j-core:2.11.0"
    implementation "org.prelle:javafx-extensions:4.1.2"
    implementation "de.rpgframework:rpgframework-core:4.1.0-SNAPSHOT"
    implementation "de.rpgframework:rpgframework-chars:4.1.0-SNAPSHOT"
    implementation "de.rpgframework:rpgframework-javafx:4.1.0-SNAPSHOT"
    implementation "de.rpgframework:rpgframework-print:4.1.0-SNAPSHOT"
    implementation "de.rpgframework:rpgframework-products:4.1.0-SNAPSHOT"
    runtimeOnly "org.prelle:simple-persist:3.2.0-SNAPSHOT"
    runtimeOnly "com.sun.mail:javax.mail:1.6.0"
    runtimeOnly "org.prelle.rpgframework:babylon-licensecheck:3.0.0-SNAPSHOT"
    runtimeOnly "de.rpgframework:itextpdf:4.2.0"
    runtimeOnly "org.apache.pdfbox:pdfbox:2.0.18"
    runtimeOnly "org.apache.pdfbox:fontbox:2.0.18"
    runtimeOnly "commons-logging:commons-logging:1.2" 
    runtimeOnly "com.google.code.gson:gson:2.8.6"
}

javafx {
    version = '14.0.2.1'
    modules = ['javafx.controls', 'javafx.media', 'javafx.web', 'javafx.fxml']
}

sourceCompatibility = 11
targetCompatibility = 11

version=rootProject.file('version.txt').text.trim()

//mainClassName = "${ext.appModuleName}/${ext.appMainClass}"
mainClassName = "${ext.appMainClass}"

def defaultEncoding = 'UTF-8'
[compileJava, compileTestJava]*.options*.encoding = defaultEncoding

jar {
    manifest {
        attributes( 
          'Main-Class': mainClassName,
          'Implementation-Vendor': 'Stefan Prelle',
          'Implementation-Version': archiveVersion
          )
    }
}

ext.os = org.gradle.internal.os.OperatingSystem.current()

application {
    executableDir = ""
    applicationDefaultJvmArgs = [ "-Dprism.allowhidpi=false",
                                  "-Dglass.win.uiScale=96dpi"]
    if (profile == 'development') {
      applicationDefaultJvmArgs = [ "-Dprism.allowhidpi=false",
                                  "-Dglass.win.uiScale=96dpi",
                                  "-Dprofile=development"]
    } else
    if (profile == 'staging') {
      applicationDefaultJvmArgs = [ "-Dprism.allowhidpi=false",
                                  "-Dglass.win.uiScale=96dpi",
                                  "-Dprofile=staging"]
    } else
    if (profile == 'production') {
      applicationDefaultJvmArgs = [ "-Dprism.allowhidpi=false",
                                  "-Dglass.win.uiScale=96dpi",
                                  "-Dprofile=production"]
    }
}

jlink {
    imageZip = project.file("${buildDir}/distributions/${appPackaging}-${profile}-${javafx.platform.classifier}.zip")
    options = ['--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = "${appLauncher}"
        jvmArgs = ["-Dapplication.version=$version","-Dprofile=development"]
    }
	secondaryLauncher {
        name = 'Genesis-Development-HiDPI'
        mainClass = "org.prelle.genesis.Genesis5Main"
        jvmArgs = ["-Dapplication.version=$version","-Dglass.win.uiScale=112dpi","-Dprofile=development"]
    }
    forceMerge('log4j-api','itextpdf','org.apache.pdfbox','com.google.gson')
    jpackage {
        // Let the plugin detect the path to the JDK providing the jpackage tool by searching the locations indicated by:
        //  - the badass.jlink.jpackage.home system property
        //  - the BADASS_JLINK_JPACKAGE_HOME environment variable
        //  - the java.home system property
        //  - the JAVA_HOME environment variable
        //
        //   OR
        //
        // explicitly configure the below property:
        jpackageHome = '/opt/jdk-14'
        //jpackageHome = "C:/Users/Stefan/workspaces/RPGFramework/Applications/Genesis/Genesis/build/download/jdk-14"
        //
        //   OR
        //
        // download and extract a JDK that contains the jpackage tool by setting the system property 'download.jpackage':
        //if(Boolean.getBoolean('download.jpackage')) {
            jpackageHome = downloadJPackage()
        //}

        if(os.macOsX) {
            installerOptions = [
            	  '--mac-package-identifier', 'de.rpgframework.Genesis',
            	  '--mac-package-name', 'Genesis'
//                '--mac-sign',
//                '--mac-signing-key-user-name', System.getenv('SIGNING_KEY_USER_NAME'),
//                '--mac-signing-keychain', System.getenv('SIGNING_KEYCHAIN_PATH')
            ]
        } else if(os.windows) {
        	resourceDir= file("src/main/deploy/package/windows")
            installerType = 'msi' 
            installerOptions = [
            	'--win-per-user-install', 
            	'--win-menu', 
            	'--win-menu-group','RPGFramework',
            	'--win-shortcut', 
            	'--verbose',    	
            	'--win-upgrade-uuid', 'e641bb70-6cba-43bf-8070-c1894b6d46e9']
            imageOptions = ['--icon','src/main/deploy/package/windows/Genesis.ico']
         } else {
        	resourceDir= file("src/main/deploy/package/linux")
            installerType = '' // 'rpm'
            installerOptions = [
                '--linux-menu-group', 'RPGFramework', 
                '--linux-app-category', 'Game',
                '--linux-shortcut',
                '--linux-deb-maintainer', 'stefan@rpgframework.de',
                ]
            imageOptions += ['--icon', 'src/main/deploy/package/linux/Genesis.png']
        }
        installerOptions += [
            "--verbose",
            "--app-version", project.version,
            "--copyright", "Stefan Prelle",
            "--description", "A multi-system tabletop RPG generator software",
            "--vendor", "RPGFramework project",
            "--name", "Genesis-Devel"
        ]
    }
}


// #### The code below is needed only if you use the downloadJPackage() method to install the jpackage tool ####

/** @return  [url, extension, directory] */
String[] getJPackageCoordinates() {
    if(os.macOsX) return [
        'https://download.java.net/java/GA/jdk14/076bab302c7b4508975440c56f6cc26a/36/GPL/openjdk-14_osx-x64_bin.tar.gz',
        'tar.gz',
        'jdk-14.jdk/Contents/Home'
    ]
    if(os.windows) return [
        'https://download.java.net/java/GA/jdk14/076bab302c7b4508975440c56f6cc26a/36/GPL/openjdk-14_windows-x64_bin.zip',
        'zip',
        'jdk-14'
    ]
    return [
        'https://download.java.net/java/GA/jdk14/076bab302c7b4508975440c56f6cc26a/36/GPL/openjdk-14_linux-x64_bin.tar.gz',
        'tar.gz',
        'jdk-14'
    ]
}

String downloadJPackage() {
    def (url, extension, directory) = getJPackageCoordinates()
    def downloadDir = "$buildDir/download"
    tasks.jpackageImage.doFirst {
        def execExt = os.windows ? '.exe' : ''
        if(!file("$downloadDir/$directory/bin/jpackage$execExt").file) {
            def jdkArchivePath = "$downloadDir/jdk-jpackage.$extension"
            download {
                src url
                dest jdkArchivePath
                overwrite false
            }
            copy {
                from ((extension == 'tar.gz') ? tarTree(resources.gzip(jdkArchivePath)) : zipTree(jdkArchivePath))
                into downloadDir
            }
        }
    }
    return "$downloadDir/$directory"
}

eclipse {
    classpath {
        containers 'org.eclipse.buildship.core.gradleclasspathcontainer'
        file {
            whenMerged {
                entries.findAll { it.properties.kind.equals('lib') && !it.properties.path.contains("junit") }.each {
                    it.entryAttributes['module'] = 'true'
                }
                entries.findAll { it.properties.path.startsWith('org.eclipse.jdt.launching.JRE_CONTAINER') }.each {
                    it.entryAttributes['module'] = 'true'
                }

            }
        }
    }
}

